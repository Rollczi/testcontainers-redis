plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '1.0.0'
    id 'net.researchgate.release' version '2.8.1'
    id 'com.github.ben-manes.versions' version '0.38.0'
    id 'com.github.breadmoirai.github-release' version '2.2.12'
}

group = 'com.redislabs'
description = 'Testcontainers Redis'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

java {
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    compileOnly group: 'org.projectlombok', name: 'lombok', version: lombok_version
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombok_version
    implementation 'com.redislabs:mesclun:1.2.1'
    api 'org.junit.jupiter:junit-jupiter-api'
    api 'org.junit.jupiter:junit-jupiter-engine'
    api 'org.junit.jupiter:junit-jupiter-params'
    api group: 'org.testcontainers', name: 'testcontainers', version: testcontainers_version
    api group: 'org.testcontainers', name: 'junit-jupiter', version: testcontainers_version
}

test {
    useJUnitPlatform()
}

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Testcontainers Redis'
                description = 'Redis modules for Testcontainers'
                url = 'https://github.com/redis-developer/testcontainers-redis'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'jruaux'
                        name = 'Julien Ruaux'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/redis-developer/testcontainers-redis'
                    developerConnection = 'scm:git:git@github.com:redis-developer/testcontainers-redis'
                    url = 'https://github.com/redis-developer/testcontainers-redis'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}

nexusPublishing {
    repositories {
        sonatype()
    }
}

githubRelease {
    token = project.hasProperty('githubToken') ? project.property('githubToken') : ''
    owner "redis-developer"
    repo "testcontainers-redis"
    body changelog()
}

def isNonStable = { String version ->
    def nonStableKeyword = ['PREVIEW'].any { it -> version.toUpperCase().contains(it) }
    def stableKeyword = ['RELEASE', 'FINAL', 'GA', 'JRE8'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+([.-]r)?$/
    return nonStableKeyword || (!stableKeyword && !(version ==~ regex))
}

tasks.named("dependencyUpdates").configure {

    rejectVersionIf {
        isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
    }

}

afterReleaseBuild.dependsOn ":githubRelease"
afterReleaseBuild.dependsOn ":publishToSonatype"
afterReleaseBuild.dependsOn ":closeAndReleaseSonatypeStagingRepository"